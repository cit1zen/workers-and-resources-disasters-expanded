// Fuel tank explosion caused by vapors in the air
defineVariable(float, _fuel_tank_explosion_size);
defineVariable(Building, _fuel_tank_building_local);
defineVariable(Resources, _fuel_tank_building_resources_actual_local);
defineVariable(Resources, _fuel_tank_building_resources_capacity_local);


// Valid fuel tank contains between 1 - 20% of fuel capacity 
//
defineFunction(CheckIfValidForFuelTankExplosion,
    int,
    Building: _fuel_tank_building,
    Resources: _fuel_tank_building_resources_actual,
    Resources: _fuel_tank_building_resources_capacity,
) {
    if (CheckIfValidExplosionTarget(
        _fuel_tank_building_resources_actual.oil,
        _fuel_tank_building_resources_capacity.oil,
        0.01,
        0.2,
        _fuel_tank_building.nType,
        BUILDINGTYPE_STORAGE
    )) {
        return(1);
    }
    if (CheckIfValidExplosionTarget(
        _fuel_tank_building_resources_actual.fuel,
        _fuel_tank_building_resources_capacity.fuel,
        0.05,
        0.4,
        _fuel_tank_building.nType,
        BUILDINGTYPE_STORAGE
    )) {
        return(1);
    }
    return(0);
}


defineFunction(SimulateFuelTankExplosion,
    void,
    int: _fuel_tank_building_index,
) {
    // Get DATA
    _fuel_tank_building_local.GetDataByIndex(_fuel_tank_building_index);
    _fuel_tank_building_resources_actual_local.GetFromBuilding(_fuel_tank_building_index);
    _fuel_tank_building_resources_capacity_local.GetCapacityFromBuilding(_fuel_tank_building_index);

    // Determine explosion size
	// Gasoline and crude oil energy density from https://energyeducation.ca/encyclopedia/Energy_density
    _fuel_tank_explosion_size = DetermineIgnitionRadiusOfExplosion(
        _fuel_tank_building_resources_actual_local.oil,
        _fuel_tank_building_resources_capacity_local.oil,
        44000.0
    );
    _fuel_tank_explosion_size = _fuel_tank_explosion_size + DetermineIgnitionRadiusOfExplosion(
        _fuel_tank_building_resources_actual_local.fuel,
        _fuel_tank_building_resources_capacity_local.fuel,
        46000.0
    );
	if (_fuel_tank_explosion_size < 1.0) {
	 	returnVoid();
	}

	// Create notification
	Notification_CreateNewStringPic(
		"Fuel tank explosion!",
		"Vapors in a tank caused an explosion that set the tank and the nearby buildings on fire.",
		"icons/fuel_tank_explosion.jpg",
		_fuel_tank_building_local.vPosition
	);

	// Start fires
	Explode(_fuel_tank_building_local, _fuel_tank_explosion_size);

    returnVoid();
}
